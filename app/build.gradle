apply plugin: 'com.android.application'
apply plugin: 'retrolambda'

ext {
    APPLICATION_ID = "com.flatstack.android"
    TARGET_SDK_VERSION = 20
    BUILD_TOOLS_VERSION = '20'

    IS_TRAVIS = "true".equals(System.getenv("TRAVIS"))
    PRE_DEX_ENABLED = "true".equals(System.getProperty("pre-dex", "true"))

    commitMessage = 'git log -1 --pretty=%B'.execute().text.trim()

    DAGGER_VERSION = '1.2.2'
    ESPERANDRO_VERSION = '2.1.0'
}

android {
    compileSdkVersion TARGET_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion TARGET_SDK_VERSION

        applicationId APPLICATION_ID
        versionCode 1
        versionName '0.1.0'
    }

    productFlavors {
        staging {
            buildConfigField "String", "API_URL", "\"https://example-staging.com\""
            applicationId "${APPLICATION_ID}.staging"
        }

        production {
            buildConfigField "String", "API_URL", "\"https://example.com\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries = PRE_DEX_ENABLED && !IS_TRAVIS
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        enable 'IconExpectedSize', 'EasterEgg', 'SelectableText', 'StopShip', 'TypographyQuotes', 'UnusedIds'
        disable 'InvalidPackage', 'GradleDependency'
        warningsAsErrors true
        lintConfig file("$projectDir/lint.xml")
    }
}

dependencies {
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:1.6.0' // java 8

    compile 'com.netflix.rxjava:rxjava-android:0.20.4' // asyncrony and events

    compile 'com.jakewharton:butterknife:5.1.2' // view injection
    compile 'com.jakewharton.timber:timber:2.4.1' // logging

    compile "com.squareup.dagger:dagger:$DAGGER_VERSION" // DI
    provided "com.squareup.dagger:dagger-compiler:$DAGGER_VERSION"
    compile 'com.squareup.picasso:picasso:2.3.4' // image loading
    compile 'com.squareup.okhttp:okhttp:2.0.0' // HTTP client
    compile 'com.squareup.retrofit:converter-jackson:1.6.1'
    compile 'com.intellij:annotations:12.0' // @Nullable @NotNull

    compile 'org.projectlombok:lombok:1.14.8' // POJO boilerplate

    compile 'nl.qbusict:cupboard:2.0.1' // sql boilerplate

    compile files('libs/argument-1.0.3.jar') // fragment arguments boilerplate

    compile 'com.github.mttkay.memento:memento:0.2' // retaining objects in activity
    provided 'com.github.mttkay.memento:memento-processor:0.2'

    provided "de.devland.esperandro:esperandro:$ESPERANDRO_VERSION" // shared prefs boilerplate
    compile "de.devland.esperandro:esperandro-api:$ESPERANDRO_VERSION"
    compile "de.devland.esperandro:esperandro-jackson-addon:$ESPERANDRO_VERSION"

    compile 'uk.co.chrisjenx.calligraphy:calligraphy:0.6.0' // custom fonts made easy
}

configurations {
    compile.exclude module: 'gson' // we prefer jackson over gson for more convinient configuration
    compile.exclude module: 'guava' // some deps drag this monster into the final APK
}