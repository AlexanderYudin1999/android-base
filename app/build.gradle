apply plugin: 'android-sdk-manager'
apply plugin: 'android'
apply plugin: 'android-apt'
apply plugin: 'retrolambda'
apply plugin: 'android-test'

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 19
    buildToolsVersion '19.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19

        versionCode 1
        versionName '0.1.0'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries = preDexEnabled && !isTravis
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        enable 'IconExpectedSize', 'EasterEgg', 'SelectableText', 'StopShip', 'TypographyQuotes', 'UnusedIds'
        disable 'InvalidPackage'
        warningsAsErrors true
    }

    sourceSets {
        main {
            androidTest.setRoot('src/test')
        }
    }
}

retrolambda {
    jdk System.getenv("JAVA8_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
}

repositories {
    // crashlytics
    maven { url 'http://download.crashlytics.com/maven' }
}

dependencies {
    compile 'com.netflix.rxjava:rxjava-android:0.19.+' // FRP

    compile 'com.jakewharton:butterknife:5.+' // view injection
    compile 'com.jakewharton.timber:timber:2.+' // logging

    compile 'com.squareup.dagger:dagger:1.+' // DI
    apt 'com.squareup.dagger:dagger-compiler:1.+'
    compile 'com.squareup.picasso:picasso:2.+' // image loading
    compile 'com.squareup.okhttp:okhttp:2.+' // HTTP client
    compile 'com.squareup.retrofit:retrofit:1.+'
    compile 'com.intellij:annotations:12.+' // @Nullable @NotNull

    compile 'org.projectlombok:lombok:1.+' // POJO boilerplate

    compile 'nl.qbusict:cupboard:2.+' // sql boilerplate

    compile files('libs/argument-1.0.3.jar') // fragment arguments boilerplate

    compile 'com.github.mttkay.memento:memento:0.+' // retaining objects in activity
    apt 'com.github.mttkay.memento:memento-processor:0.+'

    apt 'de.devland.esperandro:esperandro:2.+' // shared prefs boilerplate
    compile 'de.devland.esperandro:esperandro-api:2.+'
    compile 'de.devland.esperandro:esperandro-gson-addon:2.+'

    compile 'uk.co.chrisjenx.calligraphy:calligraphy:0.+' // custom fonts made easy

    androidTestCompile 'com.android.support:support-v4:+'
    androidTestCompile 'com.squareup.dagger:dagger-compiler:1.+'
    androidTestCompile 'junit:junit:4.+'
    androidTestCompile 'org.mockito:mockito-all:1.+'
    androidTestCompile 'org.robolectric:robolectric:2.+'
    androidTestCompile 'com.squareup:fest-android:1.+'
}